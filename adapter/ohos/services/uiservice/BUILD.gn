# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import(
    "//foundation/arkui/ace_engine/adapter/ohos/services/uiservice/uiservicems.gni")

ohos_prebuilt_etc("ui_service.cfg") {
  relative_install_dir = "init"
  source = "ui_service.cfg"
  part_name = ace_engine_part
  subsystem_name = "arkui"
}

group("uiservice_target") {
  deps = [ ":uiservice" ]
}

group("unittest") {
  testonly = true

  deps = [ "test:unittest" ]
}

config("uiservicems_config") {
  include_dirs = [
    "include/",
    "$ability_runtime_inner_api_path/base/include",
    "$ability_runtime_services_path/common/include",
    "$ace_root/adapter/ohos/services/uiservice",
    "$ace_root/adapter/ohos/services/uiservice/uiservice_plugins",
    "//commonlibrary/c_utils/base/include",
    "//utils/system/safwk/native/include",
    "//prebuilts/jdk/jdk8/linux-x86/include",
    "//prebuilts/jdk/jdk8/linux-x86/include/linux",
    "//foundation/arkui/ace_engine/interfaces/inner_api/ui_service_manager/include",
    "//foundation/windowmanager/interfaces/innerkits",
  ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

template("uiservice_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    part_name = ace_engine_part
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    sources = uiservice_files
    configs = [
      ":uiservicems_config",
      "$ace_root:ace_config",
    ]

    deps = [
      "//foundation/arkui/ace_engine/adapter/ohos/services/uiservice/uiservice_plugins/uiservice_dialog:uiservicedialog",
      "//foundation/arkui/ace_engine/interfaces/inner_api/ui_service_manager:ui_service_mgr",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
      "//foundation/systemabilitymgr/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    ]

    deps += [ "$ace_root/adapter/ohos/services/uiservice/dialog_ui/dialog_picker/js:dialog_picker_js_files_etc" ]

    external_deps = [
      "ability_base:want",
      "ability_base:zuri",
      "ability_runtime:ability_manager",
      "ability_runtime:runtime",
      "appspawn:appspawn_socket_client",
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
      "c_utils:utils",
      "eventhandler:libeventhandler",
      "hiviewdfx_hilog_native:libhilog",
      "ipc:ipc_core",
      "window_manager:libwm",
    ]
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ohos") {
    # generate uiservice_static lib
    uiservice_static("uiservice_static_" + item.name) {
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      }
    }
  }
}

ohos_shared_library("uiservice") {
  deps = [ ":uiservice_static_ohos" ]
  part_name = ace_engine_part
  subsystem_name = "arkui"
}
